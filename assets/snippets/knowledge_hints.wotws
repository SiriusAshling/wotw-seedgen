#category("Hints")
#name("Knowledge Hints")
#description("Interactively displays useful hints on niche randomizer knowledge while playing the seed")

// TODO explain key hints?

on tick {
    set_integer("show_message", 0)
    if is_in_hitbox(127, -4306, 183, -4268) && EastHollow.DepthsOpen == false && skills.glide == false set_integer("show_message", 1)
    if is_in_hitbox(-527, -4033, -461, -4018) && LowerReach.BearSneezed == false && skills.flap == false set_integer("show_message", 2)
    if is_in_hitbox(-98, -4245, -60, -4205) && EastHollow.ForestsVoice == false set_integer("show_message", 3)

    if get_integer("show_message") > 0 && get_boolean("showing_message") == false {
        if get_integer("show_message") == 1 item_message("#Motay's Note#\nThe moki will only open the entrance once you find " + skill_string(Skill::Glide))
        if get_integer("show_message") == 2 item_message("#Motay's Note#\nThe bear will only wake if you use " + skill_string(Skill::Flap))
        if get_integer("show_message") == 3 && (kwolokGroupDescriptor.hornBugBossState < 1 || 3 < kwolokGroupDescriptor.hornBugBossState) {
            item_message("#Motay's Note#\nThe moki will only open the door to Glades once you get the #Voice of the Forest# from Kwolok") // TODO placement is still pretty bad, don't have this too close to the vine
        }
        set_boolean("showing_message", true)
    }
    if get_integer("show_message") == 0 && get_boolean("showing_message") == true {
        set_boolean("showing_message", false)
    }
}

!state(regen_message_shown, Boolean)
on tick if
    (
        current_zone() == Zone::Woods
        || current_zone() == Zone::Reach
        || current_zone() == Zone::Depths
        || current_zone() == Zone::Pools
        || current_zone() == Zone::Wastes
        || current_zone() == Zone::Ruins
        || current_zone() == Zone::Willow
    )
    && is_in_hitbox(-486, -3773, -476, -3763) == false
    && skills.regenerate == false
        store(regen_message_shown, true)
on regen_message_shown item_message("#Motay's Note#\nIn Moki and Gorlek, entering zones after #The Wellspring# won't be required until you find " + skill_string(Skill::Regenerate))

// TODO interface the settings to check the difficulty
on LowerReach.TeleporterActive if skills.flap && is_in_hitbox(-284, -3971, -223, -3945) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::Reach) + " won't be required until you find " + skill_string(Skill::Flap))
on UpperDepths.TeleporterActive if skills.glide && is_in_hitbox(499, -4381, 561, -4335) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::Depths) + " won't be required until you find " + skill_string(Skill::Glide))
on EastPools.TeleporterActive if randoState.cleanWater && is_in_hitbox(-1334, -4160, -1280, -4121) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::CentralPools) + " won't be required until you find " + clean_water_string())
on WestPools.TeleporterActive if randoState.cleanWater && is_in_hitbox(-1677, -4175, -1625, -4156) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::PoolsBoss) + " won't be required until you find " + clean_water_string())
on LowerWastes.CentralTeleporterActive if skills.burrow && is_in_hitbox(1946, -3910, 2018, -3857) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::CentralWastes) + " won't be required until you find " + skill_string(Skill::Burrow))
on UpperWastes.TeleporterActive if skills.burrow && is_in_hitbox(2013, -3690, 2067, -3658) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::OuterRuins) + " won't be required until you find " + skill_string(Skill::Burrow))
on WindtornRuins.TeleporterActive if skills.burrow && is_in_hitbox(2106, -4001, 2142, -3972) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::InnerRuins) + " won't be required until you find " + skill_string(Skill::Burrow))
on WillowsEnd.InnerTeleporterActive if skills.burrow && is_in_hitbox(400, 3870, 447, -3847) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::Willow) + " won't be required until you find " + skill_string(Skill::Burrow))
on MidnightBurrows.TeleporterActive if skills.bash && is_in_hitbox(-960, -4586, -931, -4564) == false
    item_message("#Motay's Note#\nUsing the " + teleporter_string(Teleporter::Burrows) + " won't be required until you find " + skill_string(Skill::Bash))
