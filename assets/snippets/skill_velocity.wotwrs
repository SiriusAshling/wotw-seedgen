#category("Item Pool")
#name("Skill Velocity")
#description("Adds Skill Velocity pickups into the item pool that make you more speedy")

!config(count, "How many Skill Velocities to shuffle in", Integer, 3)

!state(enabled, Boolean)
!state(collected, Integer)

!include("bonus_core")
!use("bonus_core", update_bonus_wheel)

!use_icon(skill_velocity_icon, "assets/icons/bonus/skillvelocity.png")

!on_callback("bonus_core", any_collected, if collected > 0 set_boolean("any_collected", true))
!on_callback("bonus_core", any_enabled, if enabled set_boolean("any_enabled", true))
!on_callback("bonus_core", enable_all, if enabled == false enable())
!on_callback("bonus_core", disable_all, if enabled disable())

fun skill_velocity() {
    item_message("#Skill Velocity x" + (collected + 1) + "#")
    store(collected, collected + 1)
}

!add(skill_velocity(), count)
!item_data(skill_velocity(),
    "#Skill Velocity#",
    200,
    "Move faster when using abilities such as Dash and Bash",
    skill_velocity_icon,
    MapIcon::BonusItem
)

fun enable() {
    store(enabled, true)
    set_wheel_item_disable()
    update_bonus_wheel()
    store(randoUpgrades.launchSpeed, collected * 0.16)
    store(randoUpgrades.dashDistance, collected * 0.16)
    store(randoUpgrades.bashSpeed, collected * 0.16)
    store(randoUpgrades.burrowDashSpeed, collected * 0.16)
    store(randoUpgrades.waterDashSpeed, collected * 0.16)
    store(randoUpgrades.grappleSpeed, collected * 0.16)
}
fun disable() {
    store(enabled, false)
    set_wheel_item_enable()
    update_bonus_wheel()
    store(randoUpgrades.launchSpeed, 0)
    store(randoUpgrades.dashDistance, 0)
    store(randoUpgrades.bashSpeed, 0)
    store(randoUpgrades.burrowDashSpeed, 0)
    store(randoUpgrades.waterDashSpeed, 0)
    store(randoUpgrades.grappleSpeed, 0)
}

on reload {
    if collected > 0 {
        create_wheel_item()
    }
}
on collected == 1 create_wheel_item()

fun create_wheel_item() {
    set_wheel_item_name("bonus_upgrade", WheelItemPosition::Top, "Toggle Skill Velocity")
    set_wheel_item_icon("bonus_upgrade", WheelItemPosition::Top, skill_velocity_icon)
    if enabled {
        set_wheel_item_disable()
    }
    if enabled == false {
        set_wheel_item_enable()
    }
}
fun set_wheel_item_disable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::Top, "[Ability1]Toggle off")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::Top, 255, 255, 255, 255)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::Top, WheelBind::All, disable())
}
fun set_wheel_item_enable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::Top, "[Ability1]Toggle on")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::Top, 128, 128, 128, 196)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::Top, WheelBind::All, enable())
}
