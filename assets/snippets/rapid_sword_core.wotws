#hide
#name("Rapid Sword Core")
#description("Adds support for toggleable Rapid Sword")

!state(enabled, Boolean)
!state(collected, Integer)

!include("bonus_core")
!import("bonus_core", update_bonus_wheel)

!builtin_icon(rapid_sword_icon, "assets/icons/bonus/rapidsword.png")

!on_event("bonus_core", any_collected, if collected > 0 set_boolean("any_collected", true))
!on_event("bonus_core", any_enabled, if enabled set_boolean("any_enabled", true))
!on_event("bonus_core", enable_all, if enabled == false enable())
!on_event("bonus_core", disable_all, if enabled disable())

fun rapid_sword() {
    item_message("#Rapid Sword x" + (collected + 1) + "#")
    store(collected, collected + 1)
}
!export(rapid_sword)

!item_data(rapid_sword(),
    "#Rapid Sword#",
    200,
    "Sword attacks are faster",
    rapid_sword_icon,
    MapIcon::BonusItem
)

fun enable() {
    store(enabled, true)
    set_wheel_item_disable()
    update_bonus_wheel()
    store(randoUpgrades.swordSpeedMultiplier, collected * 0.25)
}
fun disable() {
    store(enabled, false)
    set_wheel_item_enable()
    update_bonus_wheel()
    store(randoUpgrades.swordSpeedMultiplier, 0)
}

on reload {
    if collected > 0 {
        create_wheel_item()
    }
}
on collected == 1 create_wheel_item()

fun create_wheel_item() {
    set_wheel_item_name("bonus_upgrade", WheelItemPosition::Right, "Toggle Rapid Sword")
    set_wheel_item_icon("bonus_upgrade", WheelItemPosition::Right, rapid_sword_icon)
    if enabled {
        set_wheel_item_disable()
    }
    if enabled == false {
        set_wheel_item_enable()
    }
}
fun set_wheel_item_disable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::Right, "[Ability1]Toggle off")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::Right, 255, 255, 255, 255)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::Right, WheelBind::All, disable())
}
fun set_wheel_item_enable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::Right, "[Ability1]Toggle on")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::Right, 128, 128, 128, 196)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::Right, WheelBind::All, enable())
}
