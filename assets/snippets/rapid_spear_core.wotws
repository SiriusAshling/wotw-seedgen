#hide
#name("Rapid Spear Core")
#description("Adds support for toggleable Rapid Spear")

!state(enabled, Boolean)
!state(collected, Integer)

!include("bonus_core")
!use("bonus_core", update_bonus_wheel)

!use_icon(rapid_spear_icon, "assets/icons/bonus/rapidspear.png")

!on_event("bonus_core", any_collected, if collected > 0 set_boolean("any_collected", true))
!on_event("bonus_core", any_enabled, if enabled set_boolean("any_enabled", true))
!on_event("bonus_core", enable_all, if enabled == false enable())
!on_event("bonus_core", disable_all, if enabled disable())

fun rapid_spear() {
    item_message("#Rapid Spear x" + (collected + 1) + "#")
    store(collected, collected + 1)
}
!share(rapid_spear)

!item_data(rapid_spear(),
    "#Rapid Spear#",
    200,
    "Spear attacks are faster",
    rapid_spear_icon,
    MapIcon::BonusItem
)

fun enable() {
    store(enabled, true)
    set_wheel_item_disable()
    update_bonus_wheel()
    store(randoUpgrades.spearSpeedMultiplier, collected * 0.4)
}
fun disable() {
    store(enabled, false)
    set_wheel_item_enable()
    update_bonus_wheel()
    store(randoUpgrades.spearSpeedMultiplier, 0)
}

on reload {
    if collected > 0 {
        create_wheel_item()
    }
}
on collected == 1 create_wheel_item()

fun create_wheel_item() {
    set_wheel_item_name("bonus_upgrade", WheelItemPosition::RightBottom, "Toggle Rapid Spear")
    set_wheel_item_icon("bonus_upgrade", WheelItemPosition::RightBottom, rapid_spear_icon)
    if enabled {
        set_wheel_item_disable()
    }
    if enabled == false {
        set_wheel_item_enable()
    }
}
fun set_wheel_item_disable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::RightBottom, "[Ability1]Toggle off")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::RightBottom, 255, 255, 255, 255)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::RightBottom, WheelBind::All, disable())
}
fun set_wheel_item_enable() {
    set_wheel_item_description("bonus_upgrade", WheelItemPosition::RightBottom, "[Ability1]Toggle on")
    set_wheel_item_color("bonus_upgrade", WheelItemPosition::RightBottom, 128, 128, 128, 196)
    set_wheel_item_action("bonus_upgrade", WheelItemPosition::RightBottom, WheelBind::All, enable())
}
